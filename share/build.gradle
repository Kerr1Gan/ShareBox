import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'AndResGuard'

andResGuard {
    // mappingFile = file("./resource_mapping.txt")
    mappingFile = null
    use7zip = true
    useSign = true
    // 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
    keepRoot = false
    whiteList = [
            // for your icon
            "R.drawable.icon",
            // for fabric
            "R.string.com.crashlytics.*",
            // for google-services
            "R.string.google_app_id",
            "R.string.gcm_defaultSenderId",
            "R.string.default_web_client_id",
            "R.string.ga_trackingId",
            "R.string.firebase_database_url",
            "R.string.google_api_key",
            "R.string.google_crash_reporting_api_key"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.15'
        //path = "/usr/local/bin/7za"
    }

    /**
     * 可选： 如果不设置则会默认覆盖assemble输出的apk
     **/
    // finalApkBackupPath = "${project.rootDir}/final.apk"

    /**
     * 可选: 指定v1签名时生成jar文件的摘要算法
     * 默认值为“SHA-1”
     **/
    // digestalg = "SHA-256"
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.2'
    defaultConfig {
        applicationId "com.ecjtu.sharebox"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 18
        versionName "1.1.18"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
//        ndk {
//            abiFilters 'armeabi-v7a'
////            abiFilters 'armeabi-v7a', 'x86'
//        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias "${signing_keyAlias}"
            keyPassword "${signging_certificatePassword}"
            storeFile file("../keystore/${signing_certificate}")
            storePassword "${signging_storePassword}"

            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            ndk {
//                abiFilters 'armeabi-v7a'
                abiFilters 'armeabi-v7a', 'x86'
            }
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            //Zipalign优化
            zipAlignEnabled true
            shrinkResources true

            debuggable false

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def path = "../../../"
                    def outputFile = output.outputFile
                    def fileName = outputFile.name.replace("-", "_")
//                        .replace("app", "app_${variant.name}")
                            .replace("release.apk", "_v${variant.versionName}_${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}_${variant.versionCode}_release.apk")
                    output.outputFileName = fileName
                }
            }

            ndk {
                abiFilters 'armeabi-v7a'
//            abiFilters 'armeabi-v7a', 'x86'
            }
        }
    }

    // can't config the resValue using "buildTypes inside of flavor"!
    // only buildConfigField seems supported!
    // therefore, resolve the problem using code below
    applicationVariants.all { variant ->
        if (variant.buildType.name == 'debug') {
            if (variant.mergedFlavor.resValues.get("app_name") != null) {
                def appName = variant.mergedFlavor.resValues.get("app_name").value
                variant.resValue('string', 'app_name', appName + ' Debug')
            }
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //compile 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation project(':channellibrary')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    //glide
    //compile 'com.github.bumptech.glide:glide:3.6.1'
    implementation project(':circularprogresslibrary')
    implementation project(':materialstepper')
    implementation project(':ijkplayer-exo')
    implementation 'com.github.bumptech.glide:glide:4.0.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:support-vector-drawable:26.1.0'
    implementation 'com.tencent.bugly:crashreport:2.8.6.0'
    testCompile 'junit:junit:4.12'
    implementation(name: 'easyserver-release', ext: 'aar')
    implementation 'com.google.firebase:firebase-ads:17.1.3'
    //    compile project(':easyserver')
    implementation project(':netcore')
    implementation project(':common-components')
    implementation project(':parcel')
    implementation project(':qrcode')
    implementation 'com.android.support:multidex:1.0.1'
    implementation project(':mvp-dagger')

    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    implementation 'com.google.code.gson:gson:2.8.5'

    // leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    //room
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    kapt 'android.arch.persistence.room:compiler:1.1.1'

    // firebase analytics
    implementation 'com.google.firebase:firebase-core:16.0.7'
    // gms analytics
    implementation 'com.google.android.gms:play-services-analytics:16.0.7'
    // FCM: firebase msg
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-messaging:17.4.0'
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.google.gms.google-services'

